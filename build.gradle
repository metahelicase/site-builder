plugins {
    id 'groovy'
    id 'com.gradle.plugin-publish' version '0.9.4'
}

group = 'org.metahelicase'
version = '1.1-SNAPSHOT'

repositories {
    jcenter()
}

dependencies {
    compile gradleApi()
    compile localGroovy()
    testCompile 'junit:junit:4.12'
    testCompile gradleTestKit()
}

test {
    systemProperty 'plugin.test.classpath', sourceSets.main.runtimeClasspath.join('\n')
}

pluginBundle {
    website = 'http://site-builder.metahelicase.org'
    vcsUrl = 'https://github.com/metahelicase/site-builder'
    description = 'DSL for building static html pages with composable modules'
    plugins {
        site {
            id = 'org.metahelicase.site-builder'
            displayName = 'Site Builder Gradle plugin'
            tags = ['static', 'site', 'html', 'builder', 'template']
            version = project.version
        }
    }
}

groovydoc {
    overview = 'src/main/groovydoc/overview.html'
    windowTitle = 'Site Builder'
    link 'https://docs.oracle.com/javase/8/docs/api/', 'java.'
    link 'http://docs.groovy-lang.org/latest/html/gapi/', 'groovy.', 'org.codehaus.groovy.'
    link 'https://docs.gradle.org/current/javadoc/', 'org.gradle.'
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task groovydocJar(type: Jar, dependsOn: groovydoc) {
    classifier = 'groovydoc'
    from groovydoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives groovydocJar
}
