---
title: Site Builder
layout: default
---

# Site Builder Gradle Plugin

Build HTML documents with modular snippets and templates written in groovy.

## How It Works

The Site Builder gradle plugin is a static site generation tool.
It's goal is to avoid writing HTML pages directly, but at the same time to let an high degree of site customization.

HTML documents have often repeated structures, like headers, footers, images with captions and section layouts.
Most tools define a custom markup language inside HTML that can be used to include external snippets and to configure templates.
They also define a context within every page, so variable usages are substituted with their value when the document is built.

Site Builder inverts this approach: the only language used to structure documents is groovy.
HTML tags are inserted into the document by using groovy method calls, where the method name is the tag name and the method arguments are the tag's attributes and content.
You can use an entire programming language to define HTML documents in a cleaner and more concise way.

## Tag Definitions

The following script shows examples of how HTML tags are translated from groovy method calls.

{% highlight groovy %}
// empty tag
br()  // <br>

// content tag
p 'some text'  // <p>some text</p>

// content tag with empty text
span ''  // <span></span>

// tag attributes
a(href: '#', 'a link')  // <a href="#">a link</a>

// attributes can be empty
input(disabled: '')  // <input disabled="">

// null attributes are formatted as tag flags
input(disabled: null)  // <input disabled>

// tag method name containing illegal characters
'!DOCTYPE html'()  // <!DOCTYPE html>
{% endhighlight %}

Tag declarations can open a subcontext by passing them a closure, where children tags can be specified.

{% highlight groovy %}
html(lang: 'en') {
    head {
        title 'Example document'
    }
    body {
        h3 'How the Site Builder works'
        p 'A not-so-detailed example.'
    }
}
{% endhighlight %}

{% highlight html %}
<html lang="en">
    <head>
        <title>Example document</title>
    </head>
    <body>
        <h3>How the Site Builder works</h3>
        <p>A not-so-detailed example.</p>
    </body>
</html>
{% endhighlight %}

## Metatags

Groovy methods can define only which tag has to be included, and what parameters or text content it has.
They cannot place a string of text without an enclosing tag, nor control how tags are formatted inside the document.

For example, you may need to place the tags with a particular layout, like in

{% highlight html %}
<h3>A Matter of <em>Layout</em></h3>
<p>
    The link <a href="/">Home</a>
    will take you to the <em>homepage</em>.
</p>
{% endhighlight %}

The Site Builder defines a set of special tags, called *metatags*, to handle this kind of requirements.

### The Text Metatag

The special tag `_` formats a string without an enclosing tag.

{% highlight groovy %}
p 'wrapped text'  // <p>wrapped text</p>

_ 'standalone text'  // standalone text
{% endhighlight %}

### The Inlining Metatag

The same `_` tag behaves differently when, instead of text, it is given a closure as subcontext.
It places every child tag on a single line, without adding any additional markup.

{% highlight groovy %}
_ { p { _ 'The ' a(href: '/', 'Home') _ ' link.' } }
{% endhighlight %}
{% highlight html %}
<p>The <a href="/">Home</a> link.</p>
{% endhighlight %}

Given these two metatags, the snippet

{% highlight html %}
<h3>A Matter of <em>Layout</em></h3>
<p>
    The link <a href="/">Home</a>
    will take you to the <em>homepage</em>.
</p>
{% endhighlight %}

can be generated by

{% highlight groovy %}
_ { h3 { _ 'A Matter of ' em 'Layout' } }
p {
    _ { _ 'The link ' a(href: '/', 'Home') }
    _ { _ 'will take you to the ' em 'homepage' _ '.' }
}
{% endhighlight %}
